// High-Risk assets with command line credentials

// identify servers and workstations that frequently run command line commands with username/password parameters. 


let MonitoredCommands = dynamic(['net', 'wmic', 'schtasks', 'psexec', 'cmdkey', 'winrs', 'pscp', 'winscp']);
DeviceEvents
| where Timestamp > ago(30d)
| where InitiatingProcessCommandLine has_any(MonitoredCommands) 
| where InitiatingProcessCommandLine matches regex '(?:(?:(?:-p)|(?:-password)|(?:-passwd)|(?:--password)|(?:--passwd)|(?:/P)|(?:/PASSWD)|(?:/PASSWORD))(?:\\s+|\\:)(?<password>(?:"((?:\\\\.|[^"\\\\])*)")|(?:[^\\s"]+)))'
or InitiatingProcessCommandLine matches regex '(?:(?:(?:-u)|(?:-user)|(?:-username)|(?:--user)|(?:--username)|(?:/u)|(?:/USER)|(?:/USERNAME))(?:\\s+|\\:)(?<username>(?:"((?:\\\\.|[^"\\\\])*)")|(?:[^\\s"]+)))'
| distinct DeviceName,InitiatingProcessCommandLine,InitiatingProcessAccountDomain;

